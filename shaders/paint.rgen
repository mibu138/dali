#version 460
#extension GL_EXT_ray_tracing : require
#extension GL_GOOGLE_include_directive : enable

#include "raycommon.glsl"

layout(binding = 0, set = 0) uniform Camera {
    mat4 model;
    mat4 view;
    mat4 proj;
    mat4 viewInv;
    mat4 projInv;
} cam;

layout(binding = 0, set = 1) uniform accelerationStructureEXT topLevelAS;
layout(binding = 1, set = 1, rgba32f) uniform image2D image;

layout(set = 2, binding = 1) uniform Brush {
    float x;
    float y;
    float r;
    int   mode;
} brush;

layout(location = 0) rayPayloadEXT hitPayload prd;

void main() 
{
    if (brush.mode == 1) // not painting
        return;
    const vec2 pixelCenter = vec2(gl_LaunchIDEXT.xy) + vec2(0.5);
    const vec2 inUV = pixelCenter / vec2(gl_LaunchSizeEXT.xy); // map to 0 to 1
    const vec2 brushPos = vec2(brush.x * 2 - 1.0, brush.y * 2 - 1.0);
    vec2 d = inUV * 2.0 - 1.0; //normalize to -1, 1 range
    d = d * brush.r * 2.0; // map to -r to r
    if (length(d) > brush.r * 2.0)
        return;
    d += brushPos;

    vec4 origin = cam.viewInv * vec4(0, 0, 0, 1);
    vec4 target = cam.projInv * vec4(d.x, d.y, 1, 1);
    vec4 dir    = cam.viewInv * vec4(normalize(target.xyz), 0);

    uint  rayFlags = gl_RayFlagsOpaqueEXT;
    float tMin     = 0.001;
    float tMax     = 10000.0;

    traceRayEXT(topLevelAS,     // acceleration structure
            rayFlags,       // rayFlags
            0xFF,           // cullMask
            0,              // sbtRecordOffset
            0,              // sbtRecordStride
            0,              // missIndex
            origin.xyz,     // ray origin
            tMin,           // ray min range
            dir.xyz,  // ray direction
            tMax,           // ray max range
            0               // payload (location = 0)
    );

    vec4 color = vec4(1, 1, 1, 1);

    ivec2 texel = ivec2(prd.hitUv * vec2(gl_LaunchSizeEXT));
    vec4 colorOrig = imageLoad(image, texel);
    color = max(color, colorOrig);
    imageStore(image, texel, vec4(0.9, 0.1, 0.2, 1.0));
}
