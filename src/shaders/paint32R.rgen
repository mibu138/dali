#version 460
#extension GL_EXT_ray_tracing : require
#extension GL_GOOGLE_include_directive : enable

#include "raycommon.glsl"
#include "common.glsl"
#include "brush.glsl"

layout(set = 0, binding = 2) uniform accelerationStructureEXT topLevelAS;

layout(set = 1, binding = 0) uniform Camera {
    mat4 model;
    mat4 view;
    mat4 proj;
    mat4 viewInv;
    mat4 projInv;
} cam;

layout(set = 1, binding = 1) uniform Block {
    Brush brush;
};

layout(set = 1, binding = 2, r32f) uniform image2D image;

layout(set = 1, binding = 3) uniform sampler2D alphaImage;

layout(location = 0) rayPayloadEXT hitPayload hit;

layout(push_constant) uniform PC {
    float seedx;
    float seedy;
    float brushx;
    float brushy;
    float angle;
} pc;

float rand(vec2 co){
    return fract(sin(dot(co.xy ,vec2(12.9898,78.233))) * 43758.5453) - 0.5;
}

#include "fireray.glsl"

vec2 samplePos(vec2 uv, float a)
{
    mat2 R = mat2(cos(a), -sin(a), sin(a), cos(a));
    uv -= vec2(0.5, 0.5);
    uv = R * uv;
    uv += vec2(0.5, 0.5);
    return uv;
}

void main() 
{
    const vec2 jitter = vec2(rand(gl_LaunchIDEXT.xy * pc.seedx), rand(gl_LaunchIDEXT.xy * pc.seedy * 41.45234));
    const vec2 pixelCenter = vec2(gl_LaunchIDEXT.xy) + vec2(0.5) + jitter;
    const vec2 inUV = pixelCenter / vec2(gl_LaunchSizeEXT.xy); // map to 0 to 1
    vec2 brushPos = vec2(pc.brushx, pc.brushy) * 2.0 - 1.0; // map to -1, 1 range
    vec2 st = inUV * 2.0 - 1.0; //normalize to -1, 1 range
    st = st * brush.radius;

    fireRay(cam.viewInv, cam.projInv, st, brushPos);

    const float dist = length(st);
    const float f = brush.anti_falloff;
    float alpha = (1.0 - smoothstep(f, brush.radius, dist)) * brush.opacity;
    float imgAlpha = texture(alphaImage, samplePos(inUV, pc.angle)).r;
    //float imgAlpha = 1.0;
    vec4 color = vec4(alpha * imgAlpha, 0, 0, 0); //spec states R component is used for r32f format images

    ivec2 texel = ivec2(hit.uv * vec2(imageSize(image)));

    imageStore(image, texel, color);
}
