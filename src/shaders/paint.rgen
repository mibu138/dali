#version 460
#extension GL_EXT_ray_tracing : require
#extension GL_GOOGLE_include_directive : enable

#include "raycommon.glsl"
#include "common.glsl"
#include "brush.glsl"

layout(set = 0, binding = 2) uniform accelerationStructureEXT topLevelAS;

layout(set = 1, binding = 0) uniform Camera {
    mat4 model;
    mat4 view;
    mat4 proj;
    mat4 viewInv;
    mat4 projInv;
} cam;

layout(set = 1, binding = 1) uniform Block {
    Brush brush;
};

layout(set = 1, binding = 2, r32f) uniform image2D image;

layout(location = 0) rayPayloadEXT hitPayload hit;

layout(push_constant) uniform PC {
    float seedx;
    float seedy;
    float brushx;
    float brushy;
} pc;

float rand(vec2 co){
    return fract(sin(dot(co.xy ,vec2(12.9898,78.233))) * 43758.5453) - 0.5;
}

#include "fireray.glsl"

void main() 
{
    //const vec2 jitter = vec2(rand(gl_LaunchIDEXT.xy * pc.seedx), rand(gl_LaunchIDEXT.xy * pc.seedy * 41.45234));
    const vec2 jitter = vec2(0, 0);
    const vec2 pixelCenter = vec2(gl_LaunchIDEXT.xy) + vec2(0.5) + jitter;
    const vec2 inUV = pixelCenter / vec2(gl_LaunchSizeEXT.xy); // map to 0 to 1
    vec2 brushPos = vec2(pc.brushx, pc.brushy) * 2.0 - 1.0; // map to -1, 1 range
    vec2 st = inUV * 2.0 - 1.0; //normalize to -1, 1 range
    st = st * brush.radius;
    const float dist = length(st);

    //fireRay(cam.viewInv, cam.projInv, st);
    mat4 viewInv = cam.viewInv;
    mat4 projInv = cam.projInv; 
    // for the origin we do the opposite of direction and zero out all but w 
    // so that we only extract the translation from the camera. (viewInv == cam.xform)
    vec4 origin = viewInv * vec4(0, 0, 0, 1);
    // see my notes on projection matrices. basically this is a short hand 
    // matrix multiple of projInv to the brush position. 
    // we only want to apply it to the brush offset in screen space 
    // to avoid squashing or stretching the shape of the brush when 
    // the projection matrix is 'non-uniform' (ie when the viewport width and 
    // height are not equal).
    st = st + vec2(projInv[0][0] * brushPos.x, projInv[1][1] * brushPos.y);
    // still a bit curious about the -1 in z but it works.
    // the 0 in w will kill off any translation in the camera, which we 
    // want since this is a direction. we normalize so that the ray 
    // length is controlled only by tMin and tMax.
    vec4 target = normalize(vec4(st.x, st.y, -1, 0));
    vec4 dir    = viewInv * target;
    //vec4 dir    = viewInv * vec4(st.x, st.y, 100, 1);

    uint  rayFlags = gl_RayFlagsOpaqueEXT;
    float tMin     = 0.001;
    float tMax     = 10000.0;

    traceRayEXT(topLevelAS, // acceleration structure
            rayFlags,       // rayFlags
            0xFF,           // cullMask
            0,              // sbtRecordOffset
            0,              // sbtRecordStride
            0,              // missIndex
            origin.xyz,     // ray origin
            tMin,           // ray min range
            dir.xyz,        // ray direction
            tMax,           // ray max range
            0               // payload (location = 0)
    );
    //fireRay(cam.viewInv, cam.projInv, st);

    const float f = brush.anti_falloff;
    float alpha = 1.0 - smoothstep(f, brush.radius, dist);
    alpha *= brush.opacity;
    vec4 color = vec4(brush.r, brush.g, brush.b, alpha);

    ivec2 texel = ivec2(hit.uv * vec2(imageSize(image)));

    imageStore(image, texel, color);
}
